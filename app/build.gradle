apply plugin: 'com.android.application'
//apply plugin: 'newlens'

android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "com.appublisher.quizbank"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 59
        versionName "2.3.0"
        multiDexEnabled true

        //参数
        manifestPlaceholders = [
                GT_APPID: rootProject.ext.GT_APPID,
        ]
    }

    signingConfigs {
        release {
            v2SigningEnabled false
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            res.srcDirs = ['src/main/res']
        }
    }

    // 渠道
    productFlavors {
        dev {
            minSdkVersion 21
            manifestPlaceholders = [APP_NAME   : "腰果公考",
                                    APP_CHANNEL: "dev"]
        }

        template {
            manifestPlaceholders = [APP_NAME   : "腰果公考",
                                    APP_CHANNEL: "default"]
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        //noinspection GroovyAssignabilityCheck
                        def apkFile = new File(
                                output.outputFile.getParent(),
                                "QuizBank_${variant.versionName}" +
                                        "_${variant.flavorName}.apk")
                        output.outputFile = apkFile
                    }
                }
            }
        }
    }
}

File propFile = file('signature.properties');
if (propFile.exists()) {
    def Properties props = new Properties()
    props.load(new FileInputStream(propFile))
    if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        android.buildTypes.release.signingConfig = null
    }
} else {
    android.buildTypes.release.signingConfig = null
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':treeviewlib')
    compile project(':diagramlib')
    compile project(':lib_basic')
    compile project(':lib_login')
    compile project(':lib_pay')
    compile project(':lib_course')
    compile 'org.apmem.tools:layouts:1.8@aar'
    compile 'com.coolerfall:android-http-download-manager:1.5.1'
    compile 'com.liulishuo.filedownloader:library:0.2.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:multidex:1.0.1'
    // 小班
    compile(name: 'multi-image-selector', ext: 'aar')
    compile 'com.android.support:design:23.2.0'
    // 阿里百川
    compile(name: 'alisdk-feedback-2.3.0', ext: 'aar')
    compile(name: 'securityguardaar3-5.1.81', ext: 'aar')
}
